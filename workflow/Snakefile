#Define samples from run_links dir using wildcards
#18AR1374_R1_trim_PE.fastq.gz
SAMPLES, = glob_wildcards("../run_links/{sample}_R1_trim_PE.fastq.gz")

localrules: bwa_index_build,qualimap_tab,bam_index
#Target rule
rule all:
    input:
        "../ref/FA1090ref.amb",
        "../ref/FA1090ref.ann",
        "../ref/FA1090ref.bwt",
        "../ref/FA1090ref.pac",
        "../ref/FA1090ref.sa",
        expand("../output/dedup/{sample}_dedup.bam", sample=SAMPLES),
        expand("../output/dedup/{sample}_marked_dedup_metrics.txt", sample=SAMPLES), 
        expand("../output/SNPcalls/{sample}_raw.vcf", sample=SAMPLES)

#Create index for reference
rule bwa_index_build:
    input:
        "../ref/Ngono_FA1090.fna"
    conda:
        "envs/bwa.yaml"
    output:
        "../ref/FA1090ref.amb",
        "../ref/FA1090ref.ann",
        "../ref/FA1090ref.bwt",
        "../ref/FA1090ref.pac",
        "../ref/FA1090ref.sa"
    shell:
        """
        bwa index -p ../ref/FA1090ref {input}
        """

#Map raw reads to the reference genome
rule bwa_map:
    input:
        BT_index = rules.bwa_index_build.output,
        R1="../run_links/{sample}_R1_trim_PE.fastq.gz",
        R2="../run_links/{sample}_R2_trim_PE.fastq.gz"
    conda:
        "./envs/bwa.yaml"
    params:
        threads=16,
        ref="../ref/FA1090ref",
        rg="@RG\\tID:{sample}\\tPL:ILLUMINA\\tPU:gono\\tSM:{sample}"
    log:
        "../logs/bwa/{sample}.log"
    output:
        "../output/sam/{sample}.sam"
    shell:
        "bwa mem -t {params.threads} {params.ref} -R '{params.rg}'z {input.R1} {input.R2} > {output} 2> {log}"

#Conver to bam file
rule samtools_view:
    input:
        "../output/sam/{sample}.sam"
    output:
        "../output/bam/{sample}.bam"
    shell:
        "samtools view -q 30 -bS {input} > {output}"

#Sort bam file according to coordinates
rule samtools_sort:
    input:
        "../output/bam/{sample}.bam"
    conda:
        "envs/bwa.yaml"
    output:
        "../output/sorted/{sample}.sorted.bam"
    shell:
        "samtools sort {input} > {output}"

#Mark and tag duplicated reads in bam files
rule picard_mark_duplicates:
    input:
        "../output/sorted/{sample}.sorted.bam"
    conda:
        "envs/picard.yaml"
    output:
        BAM="../output/dedup/{sample}_dedup.bam",
        TXT="../output/dedup/{sample}_marked_dedup_metrics.txt" 
    log:
        "../logs/picard/{sample}.log"
    shell:
        "picard MarkDuplicates -I {input} -O {output.BAM} -M {output.TXT} 2> {log}"

# collect BAM files that match the {sample} wildcard.
def qualimap_input(wildcards):
   files = expand('../output/dedup/{sample}_dedup.bam', sample=SAMPLES)
   return files

#create input file for qualimap and run multibam qc on all files
rule qualimap_tab:
    input:
        files=qualimap_input
    output:
        "../output/multibamqc/multibamqc.tab"
    shell:
        """
        for i in {input};
            do 
                id=$(basename $i .bam)
                echo -e "${{id}}\t${{i}}" >> {output}
            done
        """

#run multiqc
rule qualimap_multiqc:
    input:
        tab="../output/multibamqc/multibamqc.tab",
        gff="../ref/Ngono_FA1090_genomic.gff"
    output:
        directory("../output/multibamqc/report")
    conda:
        "envs/qualimap.yaml"
    shell:
        "qualimap multi-bamqc -r -d {input.tab} -gff {input.gff} --outdir {output}"

rule bam_index:
    input:
        bam="../output/dedup/{sample}_dedup.bam"
    conda:
        "envs/bwa.yaml"
    output:
        "../output/dedup/{sample}_dedup.bam.bai"
    shell:
        "samtools index {input.bam}"

#SNP calling FreeBayes
rule freebayes:
    input:
        ref="../ref/Ngono_FA1090.fna",
        bam="../output/dedup/{sample}_dedup.bam",
        indexes="../output/dedup/{sample}_dedup.bam.bai"
    output:
        "../output/SNPcalls/{sample}_raw.vcf"  # either .vcf or .bcf
    conda:
        "envs/freebayes.yaml"
    log:
        "../logs/freebayes/{sample}.log"
    params:
        min_map_qual=30, #default=30,
        min_cov=10, #default=0,
        ploidy=1,  #default=2,
        min_alt_fract=0.7 #default=0
    shell:
        """
        freebayes -p {params.ploidy} -! {params.min_cov} -m {params.min_map_qual} -f {input.ref} -F {params.min_alt_fract} -b {input.bam} -v {output} 1> {log}"""

#-p = ploidy default =2
# -F  --min-alternate-fraction N   Require at least this fraction of observations supporting an alternate allele within a single individual in the in order to evaluate the position.  default: 0.0