#Define samples from run_links dir using wildcards
#18AR1374_R1_trim_PE.fastq.gz
SAMPLES, = glob_wildcards("../run_links/{sample}_R1_trim_PE.fastq.gz")

localrules: qualimap_tab
#Target rule
rule all:
    input:
        "../ref/FA1090ref.amb",
        "../ref/FA1090ref.ann",
        "../ref/FA1090ref.bwt",
        "../ref/FA1090ref.pac",
        "../ref/FA1090ref.sa",
        expand("../output/dedup/{sample}_dedup.bam", sample=SAMPLES),
        expand("../output/dedup/{sample}_marked_dedup_metrics.txt", sample=SAMPLES), 
        "../output/multibamqc/report"

#Create index for reference
rule bwa_index_build:
    input:
        "../ref/Ngono_FA1090.fna"
    conda:
        "envs/bwa.yaml"
    output:
        "../ref/FA1090ref.amb",
        "../ref/FA1090ref.ann",
        "../ref/FA1090ref.bwt",
        "../ref/FA1090ref.pac",
        "../ref/FA1090ref.sa"
    shell:
        """
        bwa index -p ../ref/FA1090ref {input}
        """

#Map raw reads to the reference genome
rule bwa_map:
    input:
        BT_index = rules.bwa_index_build.output,
        R1="../run_links/{sample}_R1_trim_PE.fastq.gz",
        R2="../run_links/{sample}_R2_trim_PE.fastq.gz"
    conda:
        "./envs/bwa.yaml"
    output:
        "../output/sam/{sample}.sam"
    shell:
        "bwa mem -t 12 ../ref/FA1090ref {input.R1} {input.R2} > {output}"

#Conver to bam file
rule samtools_view:
    input:
        "../output/sam/{sample}.sam"
    output:
        "../output/bam/{sample}.bam"
    shell:
        "samtools view -q 30 -bS {input} > {output}"

#Sort bam file according to coordinates
rule samtools_sort:
    input:
        "../output/bam/{sample}.bam"
    conda:
        "envs/bwa.yaml"
    output:
        "../output/sorted/{sample}.sorted.bam"
    shell:
        "samtools sort {input} > {output}"

#Mark and tag duplicated reads in bam files
rule picard_mark_duplicates:
    input:
        "../output/sorted/{sample}.sorted.bam"
    conda:
        "envs/picard.yaml"
    output:
        BAM="../output/dedup/{sample}_dedup.bam",
        TXT="../output/dedup/{sample}_marked_dedup_metrics.txt" 
    shell:
        "picard MarkDuplicates -I {input} -O {output.BAM} -M {output.TXT}"

# collect BAM files that match the {sample} wildcard.
def qualimap_input(wildcards):
   files = expand('../output/dedup/{sample}_dedup.bam', sample=SAMPLES)
   return files

#create input file for qualimap and run multibam qc on all files
rule qualimap_tab:
    input:
        files=qualimap_input
    output:
        "../output/multibamqc/multibamqc.tab"
    shell:
        """
        for i in {input};
            do 
                id=$(basename $i .bam)
                echo -e "${{id}}\t${{i}}" >> {output}
            done
        """

#run multiqc
rule qualimap_multiqc:
    input:
        tab="../output/multibamqc/multibamqc.tab",
        gff="../ref/Ngono_FA1090_genomic.gff"
    output:
        directory("../output/multibamqc/report")
    conda:
        "envs/qualimap.yaml"
    params:
        threads=8
    shell:
        "qualimap multi-bamqc -r -d {input.tab} -gff {input.gff} --outdir {output}"


# #SNP calling
#rule FreeBayes
    # input:

    # conda:

    # output:

    # shell:

# rule mpileup
#     input:
#         "../output/bam/{sample}sorted.bam"
#     conda:
#         "envs/SNPcall.yaml"
#     output:
#         "../output/sorted/{sample}.sorted.bam"
#     shell:
#         "samtools mpileup -L1000 -d 1000 -m {input} | bcftools call -A -M -v -S"